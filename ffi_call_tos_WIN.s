;Poo Poo FFI for Windows64 to TempleOS
SECTION .text
GLOBAL FFI_CALL_TOS_0
FFI_CALL_TOS_0:
PUSH RBP
MOV RBP,RSP
AND RSP,-0x10
; In Aiwnios ABI RSI/RDI are temp registers
PUSH RSI
PUSH RDI
CALL RCX
POP RDI
POP RSI
LEAVE
RET

GLOBAL FFI_CALL_TOS_1
FFI_CALL_TOS_1:
PUSH RBP
MOV RBP,RSP
AND RSP,-0x10
; Ditto
PUSH RSI
PUSH RDI
PUSH RDX
CALL RCX
ADD RSP,8
POP RDI
POP RSI 
LEAVE
RET

GLOBAL FFI_CALL_TOS_2
FFI_CALL_TOS_2:
PUSH RBP
MOV RBP,RSP
AND RSP,-0x10
PUSH RSI 
PUSH RDI
PUSH R8
PUSH RDX
CALL RCX
ADD RSP,16
POP RDI
POP RSI 
LEAVE
RET

GLOBAL FFI_CALL_TOS_3
FFI_CALL_TOS_3:
PUSH RBP
MOV RBP,RSP
AND RSP,-0x10
PUSH RSI 
PUSH RDI
PUSH R9
PUSH R8
PUSH RDX
CALL RCX
ADD RSP,0x18
POP RDI
POP RSI
LEAVE
RET

; https://docs.microsoft.com/en-us/cpp/build/stack-usage?view=msvc-170
; Fist stack arg:0x30
; R9 HOME+0x28 
; R8 HOME+0x20
; RDX HOME+0x18
; RCX HOME+0x10
; RET ADDR+8
; RBP
GLOBAL FFI_CALL_TOS_4
FFI_CALL_TOS_4:
PUSH RBP
MOV RBP,RSP
AND RSP,-0x10
PUSH RSI
PUSH RDI
PUSH QWORD [RBP+0x30]
PUSH R9
PUSH R8
PUSH RDX
CALL RCX
ADD RSP,0x20
POP RDI
POP RSI
LEAVE
RET
